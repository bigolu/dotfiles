#!/bin/env fish

# Wrapper for fzf. If caller is inside a tmux session, it will zoom the current pane,
# run fzf, and minimize the pane. Unless the pane was zoomed before calling this script,
# in which case it will leave it zoomed.
#
# The benefit of this compared to a fullscreen tmux popup is that zoomed tmux panes
# expand and contract as the terminal window resizes, whereas popups do not.
# This way fzf will always take up the entire window, even if the window is resized.

# If we're not in TMUX just passthrough to fzf
if not set --query TMUX
  fzf $argv
  return $status
end

if tmux list-panes -F '#F' | grep -q Z
  set _pane_was_initially_zoomed
else
  tmux resize-pane -Z
end

fzf $argv
set _fzf_return_value $status

if not set --query _pane_was_initially_zoomed
  tmux resize-pane -Z
end

return $_fzf_return_value
