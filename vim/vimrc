let vim_home = $HOME . '/.vim/'

set backspace=indent,eol,start
let &fillchars = "foldopen: ,fold: ,vert:\u2502,stl:\ ,stlnc:\ "
set hidden
set laststatus=2
set showcmd
set wildmenu
set completeopt+=popup

" While typing the search query, highlight where the first match would be.
set incsearch

" persist undo history to disk
let &undodir = vim_home . 'undofiles/'
call mkdir(&undodir, "p")
set undofile

" set backup directory
let &backupdir = vim_home . 'backups/'
call mkdir(&backupdir, "p")
set backup

" So I can use alt+<lowercase_letter> mappings
for ascii_code in range(97, 122)
  let char = nr2char(ascii_code)
  execute "set <M-".char.">=\e".char
endfor

" enable mouse mode while in tmux
let &ttymouse = has('mouse_sgr') ? 'sgr' : 'xterm2'

" Re-read file if it is changed by an external program
set autoread

" Source neovim configs
let g:neovim_init = $HOME . '/.config/nvim/init.vim'
if filereadable(g:neovim_init)
  execute 'source ' . g:neovim_init
endif
let g:neovim_config_dir = $HOME . '/.config/nvim/plugin'
if isdirectory(g:neovim_config_dir)
  let g:configs = split(globpath(g:neovim_config_dir, '*'), '\n')
  for config in g:configs
    execute 'source ' . config
  endfor
endif
